int falling_block_centre_x;
int falling_block_centre_y;

char[][] board;
char[] input_buff = new char[15];

fun init_board {
    board = new char[][BOARD_HEIGHT];

    int row = 0;
    while row < BOARD_HEIGHT {
        board[row] = new char[BOARD_WIDTH];

        int column = 0;
        while column < BOARD_WIDTH {
            board[row][column] = determine_char(column, row);
            column = column + 1;
        }

        row = row + 1;
    }

    reset_falling_block();
}

fun print_board {
    int row = 0;
    while row < BOARD_HEIGHT {
        int column = 0;
        while column < BOARD_WIDTH {
            put_char_x_y(board[row][column], column, row + TOP_OFFSET);
            column = column + 1;
        }

        row = row + 1;
    }
}

fun process_frame {
    paint_falling_block(BACKGROUND_CHAR);

    int chars_read = get_input(input_buff);
    process_input(chars_read);

    if will_collide(falling_block_centre_x, falling_block_centre_y + 1) {
        paint_falling_block(BLOCK_CHAR);
        reset_falling_block();
        paint_falling_block(BLOCK_CHAR);
    } else {
        falling_block_centre_y = falling_block_centre_y + 1;
    }

    paint_falling_block(BLOCK_CHAR);
}

fun process_input(int chars_read) {
    int i = 0;
    while i < chars_read {
        char c = input_buff[i];
        int x = falling_block_centre_x;
        int y = falling_block_centre_y;

        if c == MOVE_LEFT && !will_collide(x - 1, y) {
            falling_block_centre_x = falling_block_centre_x - 1;
        }

        if c == MOVE_RIGHT && !will_collide(x + 1, y) {
            falling_block_centre_x = falling_block_centre_x + 1;
        }

        if c == MOVE_DOWN && !will_collide(x, y + 1) {
            falling_block_centre_y = falling_block_centre_y + 1;
        }

        i = i + 1;
    }
}

fun will_collide(int x, int y) => bool {
    if x <= LEFT_WALL || x >= RIGHT_WALL || y >= FLOOR {
        ret true;
    }

    ret board[y][x] != BACKGROUND_CHAR;
}

fun paint_falling_block(char c) {
    board[falling_block_centre_y][falling_block_centre_x] = c;
}

fun reset_falling_block {
    falling_block_centre_x = BOARD_WIDTH / 2;
    falling_block_centre_y = 0;
}

fun determine_char(int column, int row) => char {
    if is_wall(column, row) {
        ret WALL_CHAR;
    }

    if is_floor(column, row) {
        ret FLOOR_CHAR;
    }

    ret BACKGROUND_CHAR;
}

fun is_wall(int x, int y) => bool {
    if y == BOARD_HEIGHT {
        ret false;
    }

    ret x == LEFT_WALL || x == RIGHT_WALL;
}

fun is_floor(int x, int y) => bool {
    ret y == FLOOR;
}